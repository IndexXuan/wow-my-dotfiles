" ----------------------------------------------------------------------------
"
"                           Plugins and Settings v3.0.0
"                              Plug with 24 Plugins
"
" ----------------------------------------------------------------------------

call plug#begin('$HOME/.vim/plugged')

" ----------------------------------------------------------------------------
"
"  Vim-Plug --- Minimalist Vim Plugin Manager
"
"  command:
"  :PlugInstall[name ...]
"    install plugins
"  :PlugUpdate[name ...]
"    install or update plugins
"  :PlugClean[!]
"    remove plugin not in list(bang version will clean without prompt)
"  :PlugStatus
"    check the status of plugins(e.g. load or not)
"  :PlugUpgrade
"    upgrade vim-plug itself
"  :PlugDiff
"    examine changes from the previous update and the pending changes
"  :PlugSnapshot[!] [output path]
"    generate script for restoring the current snapshot of the plugins
"
"  options:
"  branch / tag / commit => æ’ä»¶ git ä»“åº“é€‰æ‹©
"  rtp                   => vim æ’ä»¶åŒ…å«åœ¨å­æ–‡ä»¶å¤¹
"  dir                   => æ’ä»¶è‡ªå®šä¹‰æ–‡ä»¶å¤¹
"  do                    => å®šä¹‰æ’ä»¶å®‰è£…é’©å­ï¼Œä¼ å…¥å­—ç¬¦ä¸²å‘½ä»¤æˆ–è€…å‡½æ•°å
"  on                    => æ»¡è¶³æ¡ä»¶æ‡’åŠ è½½
"  for                   => æ–‡ä»¶ç±»å‹æ‡’åŠ è½½
"  frozen                => ä¸éšç€æ›´æ–°é™¤éå…·ä½“è¡¨æ˜
"
"  keybindings
"  in :PlugStatus state, press L to load plugin in cursor
"
"  More docs in github homepage
"  https://github.com/junegunn/vim-plug
"
" ----------------------------------------------------------------------------

" Path Alias
let g:vimfiles = $HOME . '.vim/'
let g:dotfiles = $HOME . '/wow-my-dotfiles/'

" Disabled built-in Netrw, always use NerdTree
let g:loaded_netrwPlugin = 1

" UI Bar Width
let g:bar_width = 30

" è®¾ç½® statusline æ ¹æ®å±å¹•å¤§å°éšè—éƒ¨åˆ†ç»„ä»¶çš„å“åº”å¼é˜ˆå€¼(è„šæœ¬å±€éƒ¨å˜é‡)
let s:screen_xs = 30
let s:screen_sm = 60
let s:screen_md = 120
let s:screen_lg = 150
let s:screen_xl = 180

augroup lazyload
  autocmd!
  " å¿…é¡»å…ˆåŠ è½½ dev-icons å¦åˆ™ vim-startify ä¼šä¸å±•ç¤º icon
  autocmd vimEnter * call LoadAsync()
        \ | autocmd! lazyload
augroup END

function! Handler(_)
  call plug#load('vim-devicons', 'nerdtree', 'vim-nerdtree-syntax-highlight', 'vim-gitgutter', 'vim-fugitive', 'lightline.vim', 'vim-snippets')
  if !has("nvim")
    " Vim éœ€è¦æå‰åŠ è½½è¿™ä¸ªï¼Œæ‡’åŠ è½½ä¸èµ·ä½œç”¨ ...
    call plug#load('quickmenu.vim')
  endif
  execute ":GitGutterEnable"
  " vim [empty] - é¡ºåºä¸èƒ½å˜ï¼Œç²¾å¿ƒè°ƒè¯•å‡ºæ¥çš„
  if !argc()
    " Open Left & Right Bar
    call OpenBar()
    " focus on Startify
    wincmd w
    function! s:filter_header(lines) abort
      let longest_line   = max(map(copy(a:lines), 'strwidth(v:val)'))
      " NOTE: ä¿®å¤å±…ä¸­é—®é¢˜ï¼Œå‡å» bar_width * 2
      let cols =  &columns - 60 "&columns
      let centered_lines = map(copy(a:lines),
            \ 'repeat(" ", (cols / 2) - (longest_line / 2)) . v:val')
      return centered_lines
    endfunction
    let g:startify_custom_header = s:filter_header(startify#fortune#cowsay())
    " Open Startify Screen
    execute("Startify")
  endif
  " for vim [dir], call NerdTree
  autocmd StdinReadPre * let s:std_in=1
  if argc() == 1 && isdirectory(argv()[0]) && !exists("s:std_in")
    " Open Left-Side NERDTree
    exe 'NERDTree' argv()[0] | wincmd p | ene | exe 'cd '.argv()[0]
  endif
endfunction

function! OpenBar()
  " Open Right-Side Menu
  call OpenMenu()
  " Open Left-Side NERDTree
  execute("NERDTree")
endfunction

" å‡½æ•°åå¿…é¡»å¤§å†™å¼€å¤´ï¼ŒåŠ  ! ä¸ºäº†é¿å…æŠ¥é‡å¤å®šä¹‰ï¼Œå¼ºè¡Œè¦†ç›–æ‰§è¡Œ
function! LoadAsync()
  return timer_start(0, 'Handler', { 'repeat': 1 })
endfunction

" -------------------------- Base ---------------------------

" https://github.com/neoclide/coc.nvim - 0 - not-support-lazy - å¼‚æ­¥åŠ è½½ä¼šè¶…çº§æ…¢
" Intellisense engine for vim8 & neovim, full language server protocol support as VSCode
Plug 'neoclide/coc.nvim', { 'tag': '*', 'do': 'yarn install --frozen-lockfile' }

" https://github.com/wakatime/vim-wakatime - 0 - no-need-lazy - åŸºç¡€æ’ä»¶
" Vim plugin for automatic time tracking and metrics generated from your programming activity
Plug 'wakatime/vim-wakatime'

" https://github.com/kopischke/vim-stay - 0 - not-support-lazy - æ€§èƒ½ä¸ä¸Šæ¬¡ä½ç½®æœ‰å…³
" Make Vim persist editing state without fuss
Plug 'kopischke/vim-stay'

" https://github.com/sgur/vim-editorconfig - 0 - no-need-lazy - åŸºç¡€æ’ä»¶
" Yet another EditorConfig (http://editorconfig.org) plugin for vim written in vimscript only
Plug 'sgur/vim-editorconfig'

" https://github.com/tpope/vim-surround - 1 - no-need-lazy
" surround.vim: quoting/parenthesizing made simple
Plug 'tpope/vim-surround'

" https://github.com/tpope/vim-repeat - 1 - no-need-lazy
" repeat.vim: enable repeating supported plugin maps with "."
Plug 'tpope/vim-repeat'

" https://github.com/vim-scripts/open-browser.vim - 2 - lazy
" Open URI with your favorite browser from your favorite editor
Plug 'tyru/open-browser.vim', { 'on': ['<Plug>(openbrowser-smart-search)', 'OpenBrowser'] }

" -------------------------- UI Layout ---------------------------

" https://github.com/flazz/vim-colorschemes - 0 - no-need-lazy
" one colorscheme pack to rule them all!
" Plug 'flazz/vim-colorschemes'

" https://github.com/mhinz/vim-startify - 1 - not-support-lazy - ä¼šå‡ºé—®é¢˜
" ğŸ”— The fancy start screen for Vim.
Plug 'mhinz/vim-startify'

" https://github.com/ryanoasis/vim-devicons - 2 - lazy - ä½†ä¹Ÿè¦å°½å¿«åŠ è½½ï¼Œå¦åˆ™å¾ˆå¤šåœ°æ–¹å‡ºä¸æ¥
" Adds file type glyphs/icons to popular Vim plugins: NERDTree, vim-airline, Powerline, Unite, vim-startify and more
Plug 'ryanoasis/vim-devicons', { 'on': [] }

" https://github.com/itchyny/lightline.vim - 0 - lazy - å¦åˆ™ä¼šæ‹–æ…¢
" A light and configurable statusline/tabline plugin for Vim
Plug 'itchyny/lightline.vim', { 'on': [] }

" https://github.com/scrooloose/nerdtree - 0 - lazy
" a tree explorer plugin for vim
Plug 'scrooloose/nerdtree', { 'on': [] }

" https://github.com/tiagofumo/vim-nerdtree-syntax-highlight - 2 - lazy - éå¸¸å¥½çœ‹
" Extra syntax and highlight for nerdtree files
Plug 'tiagofumo/vim-nerdtree-syntax-highlight', { 'on': [] }

" https://github.com/Xuyuanp/nerdtree-git-plugin - 0 - æ²¡å•¥å¤§ç”¨
" A plugin of NERDTree showing git status
" Plug 'Xuyuanp/nerdtree-git-plugin', { 'on': [] }

" https://github.com/skywind3000/quickmenu.vim
" A nice customizable popup menu for vim 
Plug 'skywind3000/quickmenu.vim', { 'on': [] }

" ----------------- As Programmer's Editor -----------------------

" https://github.com/scrooloose/nerdcommenter - 0 - no-need-lazy
" Vim plugin for intensely orgasmic commenting
Plug 'scrooloose/nerdcommenter'

" https://github.com/ctrlpvim/ctrlp.vim - 1 - lazy - on-demand
" Active fork of kien/ctrlp.vimâ€”Fuzzy file, buffer, mru, tag, etc finder.
Plug 'ctrlpvim/ctrlp.vim', { 'on': ['CtrlP', 'CtrlPMixed', 'CtrlPMRU'] }

" https://github.com/dyng/ctrlsf.vim - 0 - lazy - on-demand
" An ack.vim alternative mimics Ctrl-Shift-F on Sublime Text 2
Plug 'dyng/ctrlsf.vim', { 'on': ['CtrlSF'] }

" https://github.com/junegunn/fzf#using-the-finder
" ğŸŒ¸ A command-line fuzzy finder
" NOTE: If installed using Homebrew
" Plug '/usr/local/opt/fzf'

" -------------------------- Working with Git --------------------------------

" https://github.com/airblade/vim-gitgutter - 0 - lazy - æ€§èƒ½å¾ˆæ…¢
" A Vim plugin which shows a git diff in the gutter (sign column) and stages/undoes hunks.
Plug 'airblade/vim-gitgutter', { 'on': [] }

" https://github.com/tpope/vim-fugitive - 1 - lazy
" fugitive.vim: A Git wrapper so awesome, it should be illegal
Plug 'tpope/vim-fugitive', { 'on': [] }

" --------------------------- Language Plugins ---------------------------------

" https://github.com/leafgarland/typescript-vim - 0 - no-need-lazy
" Typescript syntax files for Vim
Plug 'leafgarland/typescript-vim', { 'for': ['typescript'] }

" https://github.com/posva/vim-vue - 0 - no-need-lazy
" Syntax Highlight for Vue.js components
Plug 'posva/vim-vue', { 'for' : ['vue'] }

" https://github.com/iamcco/markdown-preview.nvim - no-need-lazy
" markdown preview plugin for (neo)vim
Plug 'iamcco/markdown-preview.nvim', { 'do': 'cd app & yarn install', 'for': [ 'markdown', 'md'] }

" https://github.com/Quramy/vim-js-pretty-template
" highlights JavaScript's Template Strings in other FileType syntax rule
" Plug 'Quramy/vim-js-pretty-template'

" https://github.com/maxmellon/vim-jsx-pretty
" ğŸ”¦ [Vim script] JSX syntax pretty highlighting for vim.
" Plug 'maxmellon/vim-jsx-pretty', { 'for': ['typescript.tsx', 'javascript.jsx'] }

" --------------------- Extras / Advanced ----------------------

" https://github.com/honza/vim-snippets - 1 - lazy
" vim-snipmate default snippets (Previously snipmate-snippets)
Plug 'honza/vim-snippets', { 'on': [] }

" https://github.com/Valloric/MatchTagAlways - 0 - lazy
" A Vim plugin that always highlights the enclosing html / xml tags
Plug 'Valloric/MatchTagAlways', { 'for' : ['html', 'vue'] }

" https://github.com/heavenshell/vim-jsdoc - 2 - lazy - ä¸æ”¯æŒ vue æ–‡ä»¶ï¼ŒåŠ äº†æ²¡ä¹Ÿç”¨
" Generate JSDoc to your JavaScript code.
Plug 'heavenshell/vim-jsdoc', { 'for' : ['javascript', 'typescript'] }

" Add plugins to &runtimepath
call plug#end()

" -------------------------  Plugins-Specific Settings Start -------------------------


" https://github.com/neoclide/coc.nvim {{
  " 1. global installed extensions
  " NOTE: è¿˜æœ‰äº› github vscode snippets æ²¡åˆ—å‡ºæ¥ï¼Œä¸ç¨³å®šï¼Œæœ‰çš„å®‰è£…ä¸ä¸Š
  " NOTE: æš‚æ—¶å…ˆå»æ‰äº† coc-pairs
  let g:coc_global_extensions = [
        \ 'coc-lists', 'coc-word', 'coc-emoji', 'coc-highlight', 'coc-pairs',
        \ 'coc-prettier', 'coc-tsserver', 'coc-vetur', 'coc-html', 'coc-emmet', 'coc-css', 'coc-json', 'coc-yaml',
        \ 'coc-eslint', 'coc-stylelint', 'coc-tslint-plugin',
        \ 'coc-snippets',
        \ 'https://github.com/hollowtree/vscode-vue-snippets.git#master',
        \]


  " 2. Misc
  " Use <c-space> for trigger completion.
  inoremap <silent><expr> <c-space> coc#refresh()
  "uto coc-pairs è‡ªåŠ¨åŠ ç©ºæ ¼ï¼Œéœ€è¦é…åˆ coc-settings.json é‡Œçš„å±æ€§
  inoremap <silent><expr> <CR> pumvisible() ? coc#_select_confirm() : "\<C-g>u\<CR>\<C-r>=coc#on_enter()\<CR>"
  " NOTE: coc-pairs
  autocmd FileType markdown let b:coc_pairs_disabled = ['`']
  " Highlight symbol under cursor on CursorHold
  autocmd CursorHold * silent call CocActionAsync('highlight')
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
  " autocmd CursorHold * silent call CocActionAsync('doHover')

  " 3. Using CocList
  " Show all Lists
  nnoremap <silent> <space>l  :<C-u>CocList --number-select lists<cr>
  " Show all diagnostics
  nnoremap <silent> <space>d  :<C-u>CocList --number-select --auto-preview diagnostics<cr>
  " Manage extensions
  nnoremap <silent> <space>e  :<C-u>CocList --number-select extensions<cr>
  " Show commands
  nnoremap <silent> <space>c  :<C-u>CocList --number-select commands<cr>
  " Find symbol of current document
  nnoremap <silent> <space>o  :<C-u>CocList --number-select --auto-preview outline<cr>
  " Search workspace symbols
  " nnoremap <silent> <C-p>  :<C-u>CocList --interactive --auto-preview --number-select files<cr>
  " nnoremap <silent> <leader>s  :<C-u>CocList --interactive --number-select symbols<cr>
  " Do default action for next item.
  " nnoremap <silent> <space>j  :<C-u>CocNext<CR>
  " Do default action for previous item.
  " nnoremap <silent> <space>k  :<C-u>CocPrev<CR>
  " Resume latest coc list
  nnoremap <silent> <space>r  :<C-u>CocListResume<CR>

  " 4. Buffer å†…æ“ä½œ
  " æ–‡æœ¬æœç´¢å½“å‰è¯ï¼Œç­‰åŒäº / å†…ç½®å‘½ä»¤ï¼Œä½†å¤šäº†åˆ—è¡¨èšåˆå±•ç¤º
  nnoremap <silent> <C-f> :exe 'CocList -I --normal --input='.expand('<cword>').' words'<CR>
  " æ–‡æœ¬æ¨¡ç³Šæœç´¢ï¼Œç­‰åŒäº fuzzy-search æ’ä»¶

  " 5. å…¨å±€æ“ä½œ
  " æ¨¡ç³Šæœç´¢æ–‡ä»¶ - CocList files - ä»£æ›¿ CtrlP - æš‚æœªå‘ç°ä¼˜åŠ¿
  " æ¨¡ç³Šæœç´¢æ–‡æœ¬ - æš‚æ—  - ä»£æ›¿ CtrlSF - æš‚æœªå‘ç°ä¼˜åŠ¿

  " 6. è¡¥å…¨é…ç½®
  " Use tab for trigger completion with characters ahead and navigate.
  " Use command ':verbose imap <tab>' to make sure tab is not mapped by other plugin.
  inoremap <silent><expr> <TAB>
        \ pumvisible() ? coc#_select_confirm() :
        \ coc#expandableOrJumpable() ? coc#rpc#request('doKeymap', ['snippets-expand-jump','']) :
        \ <SID>check_back_space() ? "\<TAB>" :
        \ coc#refresh()
  inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

  function! s:check_back_space() abort
    let col = col('.') - 1
    return !col || getline('.')[col - 1]  =~# '\s'
  endfunction

  let g:coc_snippet_next = '<tab>'

  " 7. è·³è½¬é…ç½®
  " core typings keymap
  " nmap <silent> <leader>pp <Plug>(coc-diagnostic-prev)
  " nmap <silent> <leader>nn <Plug>(coc-diagnostic-next)
  nmap <silent> <leader>d  <Plug>(coc-definition)
  nmap <silent> <leader>h  :call <SID>show_documentation()<CR>
  " code fix since ca is nerdtree used
  nmap <silent> <leader>cf <Plug>(coc-codeaction)
  nmap <silent> <leader>rn <Plug>(coc-rename)
  nmap <silent> <leader>rf <Plug>(coc-references)
  nmap <silent> <leader>td <Plug>(coc-type-definition)
  nmap <silent> <leader>ip <Plug>(coc-implementation)

  function! s:show_documentation()
    if &filetype == 'vim'
      execute 'h '.expand('<cword>')
    else
      call CocAction('doHover')
    endif
  endfunction

  " 8. è‡ªå®šä¹‰å‘½ä»¤
  function! SetupCommandAbbrs(from, to)
    exec 'cnoreabbrev <expr> '.a:from
          \ .' ((getcmdtype() ==# ":" && getcmdline() ==# "'.a:from.'")'
          \ .'? ("'.a:to.'") : ("'.a:from.'"))'
  endfunction

  command! -nargs=? EditVimrc :call s:EditVimrc()
  function! s:EditVimrc()
    let p = dotfiles . 'vimrc'
    if getcwd() == dotfiles . 'vimrc'
      let p = p[len(getcwd()) + 1 : ]
    endif
    execute 'edit '.p
  endfunction

  command! -nargs=? EditVimrcPlugin :call s:EditVimrcPlugin()
  function! s:EditVimrcPlugin()
    let p = dotfiles . 'vimrc.plug'
    if getcwd() == dotfiles . 'vimrc.plug'
      let p = p[len(getcwd()) + 1 : ]
    endif
    execute 'edit '.p
  endfunction

  " to open coc config
  call SetupCommandAbbrs('config', 'CocConfig')
  call SetupCommandAbbrs('restart', ':source $HOME/.vimrc')
  call SetupCommandAbbrs('vim', 'EditVimrc')
  call SetupCommandAbbrs('plugin', 'EditVimrcPlugin')
  call SetupCommandAbbrs('gb', 'Gblame')
  call SetupCommandAbbrs('gs', 'Gstatus')
  call SetupCommandAbbrs('gl', '0Glog')
  call SetupCommandAbbrs('gh', 'GitGutterLineHighlightsEnable')

  " coc.nvim - prettier
  command! -nargs=0 Prettier :CocCommand prettier.formatFile
  nmap <leader>p :Prettier<CR>
" }}


" https://github.com/zhimsel/vim-stay {{
  " for vim-stay Plug recommend set
  autocmd FileType log let b:stay_ignore=1
  " reading log 20190402
  autocmd BufReadPost *.log normal! G
  set viewoptions=cursor,folds,slash,unix
" }}


" https://github.com/vim-scripts/open-browser.vim {{
  " if it looks like URI, Open URI under cursor.
  " Otherwise, Search word under cursor.
  nmap <C-g> <Plug>(openbrowser-smart-search)
" }}


" https://github.com/mhinz/vim-startify {{
  let g:startify_bookmarks = [
        \ {'v': dotfiles . '.vimrc'},
        \ {'p': dotfiles . '.vimrc.plug'},
        \ {'z': dotfiles . '.zshrc'}
        \]
  let g:startify_change_to_dir       = 1
  let g:startify_update_oldfiles     = 1
  let g:startify_session_autoload    = 1
" }}


" https://github.com/ryanoasis/vim-devicons {{
  let g:WebnevIconsOS = 'Darwin'
  " let g:WebDevIconsNerdTreeAfterGlyphPadding = ' '
  let g:WebDevIconsUnicodeDecorateFileNodesDefaultSymbol='~'
  let g:WebDevIconsUnicodeDecorateFolderNodes = 1
  let g:DevIconsEnableFolderPatternMatching = 1
" }}


" https://github.com/itchyny/lightline.vim {{
  " NOTE: Fonts: å®‰è£… https://github.com/ryanoasis/nerd-fonts#option-4-homebrew-fonts ç„¶åç»ˆç«¯è®¾ç½®æˆè¯¥å­—ä½“ï¼Œå­—å· 14 å· ( å› ä¸º emoji icon å¤ªå¤ªï¼Œ12 å·ä¸å’Œè°ï¼Œvertical 100% and horizontal < 100% )
  " NOTE: branch icon ç­‰ from https://github.com/itchyny/lightline.vim/issues/353
  let g:lightline = {
        \ 'colorscheme': 'powerline',
        \ 'active': {
        \   'left': [
        \     [ 'mode', 'paste', 'readonly' ],
        \     [ 'ctrlpmark', 'git', 'diagnostic', 'cocstatus', 'filename', 'method' ]
        \   ],
        \   'right':[
        \     [ 'filetype', 'fileencoding', 'lineinfo', 'percent' ],
        \     [ 'coc' ]
        \   ],
        \ },
        \ 'inactive': {
        \   'left': [ ['mode', 'filename'] ],
        \   'right':[ ['lineinfo', 'percent'] ],
        \ },
        \ 'component': {
        \ },
        \ 'component_expand': {
        \   'diagnostic': 'LightlineCocDiagnostic',
        \   'cocstatus': 'LightlineCocStatus',
        \ },
        \ 'component_function': {
        \   'mode': 'LightlineMode',
        \   'readonly': 'LightLineReadonly',
        \   'ctrlpmark': 'CtrlPMark',
        \   'git': 'LightLineGit',
        \   'filename': 'LightlineFilename',
        \   'method': 'LightlineMethod',
        \   'coc': 'LightlineCoc',
        \   'filetype': 'LightlineFiletype',
        \   'fileencoding': 'LightlineFileEncoding',
        \   'lineinfo': 'LightLineLineInfo',
        \   'percent': 'LightlinePercent',
        \ },
        \ 'separator': { 'left': "\ue0b0", 'right': "\ue0b2" },
        \ 'subseparator': { 'left': "\ue0b1", 'right': "\ue0b3" }
        \ }

  function! LightlineMode()
    let fname = expand('%:t')
    return fname =~ 'NERD_tree' ? 'NERD' :
          \ fname == 'ControlP' ? 'CtrlP' :
          \ &filetype == 'ctrlsf' ? 'CtrlSF' :
          \ &filetype == 'vista_kind' ? 'Vista' :
          \ &filetype == 'quickmenu' ? 'Menu' :
          \ &filetype == 'startify' ? 'Startify' :
          \ winwidth(0) > s:screen_xs ? lightline#mode() : ''
  endfunction

  " like /etc/hosts readonly file
  function! LightLineReadonly()
    let ft = &filetype
    let fname = expand('%:t')
    let isBar = ft == 'vista_kind' || ft == 'quickmenu' || fname =~ 'NERD_tree'
    if isBar
      return ''
    endif
    if &filetype == "help"
      return ''
    elseif &readonly
      return "î‚¢"
    else
      return ''
    endif
  endfunction

  function! LightlineGitBranch()
    if exists("*fugitive#head")
      let _branch = fugitive#head()
      let parts = split(_branch, '/')
      let branch = strlen(_branch) > 12 ? parts[len(parts) - 1] : _branch
      let _ = strlen(branch) ? 'î‚  '. branch : ''
      return winwidth(0) > s:screen_md ? _ : ''
    endif
    return ''
  endfunction

  function! LightLineGitGutter()
    if ! exists('*GitGutterGetHunkSummary')
      return ''
    endif
    " let g:gitgutter_sign_added = "\u271a"
    " let g:gitgutter_sign_modified = "\u279c"
    " let g:gitgutter_sign_removed = "\u2718"
    let g:gitgutter_sign_added = "+"
    let g:gitgutter_sign_modified = "~"
    let g:gitgutter_sign_removed = "-"
    let symbols = [
          \ g:gitgutter_sign_added,
          \ g:gitgutter_sign_modified,
          \ g:gitgutter_sign_removed,
          \ ]
    let hunks = GitGutterGetHunkSummary()
    let ret = []
    for i in [0, 1, 2]
      if hunks[i] > 0
        call add(ret, symbols[i] . hunks[i])
      endif
    endfor
    let msg = join(ret, ' ')
    let _ = len(ret) ? msg : ''
    return winwidth(0) > s:screen_lg ? _ : ''
  endfunction

  function! LightLineGit()
    let branch = LightlineGitBranch()
    let gutter = LightLineGitGutter()
    " return branch . (gutter != '' ? 'â˜ ' . gutter : '')
    return branch . (gutter != '' ? ' Â¶ ' . gutter : '')
  endfunction

  function! LightlineCocDiagnostic() abort
    let info = get(b:, 'coc_diagnostic_info', {})
    if empty(info) | return '' | endif
    let msgs = []
    if get(info, 'error', 0)
      " call add(msgs, 'E' . info['error'])
      " call add(msgs, 'â¨¶ ' . info['error'])
      " call add(msgs, 'âŒ ' . info['error']) " é¢œè‰²å¾ˆå¥½ï¼Œä½†ç¬¦å·å¤ªå¤§äº†ï¼Œä¸å¤ªåè°ƒ
      call add(msgs, 'â ' . info['error'])
    endif
    if get(info, 'warning', 0)
      " call add(msgs, 'W' . info['warning'])
      " call add(msgs, 'âš  ' . info['warning'])
      call add(msgs, 'âš ï¸  ' . info['warning'])
    endif
    return join(msgs, ' ')
  endfunction

  " NOTE: å±•ç¤º ts version & prettierï¼Œæ²¡å•¥æ„ä¹‰ï¼Œæš‚æ—¶æ³¨é‡Šæ‰
  function! LightlineCocStatus() abort
    return ''
    " return get(g:, 'coc_status', '')
  endfunction

  autocmd User CocDiagnosticChange call lightline#update()

  function! LightlineFilename()
    let isHidden = &filetype == 'startify' || &filetype == 'quickmenu'
    let filename = expand('%:t') !=# '' ? expand('%:t') : '[no name]'
    let modified = &modified ? ' +' : ''
    let _  = isHidden ? '' : filename . modified . ' ' . WebDevIconsGetFileTypeSymbol()
    return winwidth(0) > s:screen_sm ? _ : ''
  endfunction

  " function! LightlineMethod()
  " let f_icon = get(g:, 'eleline_function_icon', "\uf794 ")
  " let name = get(b:, 'vista_nearest_method_or_function', '')
  " let _ = name != '' ? f_icon . name : ''
  " return winwidth(0) > s:screen_sm ? _ : ''
  " endfunction

  function! LightlineMethod()
    " å¿…é¡»åŒå¼•å·
    let f_icon = "\uf794"
    let name = get(b:, 'coc_current_function', '')
    let _ = name != '' ? f_icon . ' ' . name : ''
    return winwidth(0) > s:screen_sm ? _ : ''
  endfunction

  function! LightlineCoc()
    return winwidth(0) > s:screen_lg ? 'âš¡Powered By coc.nvim' : ''
  endfunction

  function! LightlineFiletype()
    let _ = &filetype !=# '' ? &filetype : 'no ft'
    return winwidth(0) > s:screen_lg ? _ : ''
  endfunction

  function! LightlineFileEncoding()
    let _ = &fileencoding . '[' . &fileformat . ']'
    return winwidth(0) > s:screen_lg ? _ : ''
  endfunction

  function! LightLineLineInfo()
    let _ = printf('ã‘ %d:%-2d', line('.'), col('.'))
    return winwidth(0) > s:screen_sm ? _ : ''
  endfunction

  function! LightlinePercent()
    let _ = 'Î ' . (100 * line('.') / line('$')) . '%'
    " always show percent
    return _
  endfunction

  let g:ctrlp_status_func = {
        \ 'main': 'CtrlPStatusFunc_1',
        \ 'prog': 'CtrlPStatusFunc_2',
        \ }

  " NOTE: å¼ºå¿ƒé‡å†™äº†å·¦ä¾§ï¼Œé€‚é… CtrlP
  function! CtrlPMark()
    if expand('%:t') =~ 'ControlP'
      call lightline#link('iR'[g:lightline.ctrlp_regex])
      return lightline#concatenate([g:lightline.ctrlp_prev, g:lightline.ctrlp_item
            \ , g:lightline.ctrlp_next], 0)
    else
      return ''
    endif
  endfunction

  function! CtrlPStatusFunc_1(focus, byfname, regex, prev, item, next, marked)
    let g:lightline.ctrlp_regex = a:regex
    let g:lightline.ctrlp_prev = a:prev
    let g:lightline.ctrlp_item = a:item
    let g:lightline.ctrlp_next = a:next
    return lightline#statusline(0)
  endfunction

  function! CtrlPStatusFunc_2(str)
    return lightline#statusline(0)
  endfunction
" }}


" https://github.com/scrooloose/nerdtree {{
  " Tree UI
  let g:NERDTreeWinSize = bar_width
  let g:NERDTreeDirArrowExpandable = 'â–¸'
  let g:NERDTreeDirArrowCollapsible = 'â–¾'

  " åœ¨ Handler é‡Œè®¾ç½®
  " How can I open NERDTree automatically when vim starts up on opening a directory?
  " autocmd StdinReadPre * let s:std_in=1
  " autocmd VimEnter * if argc() == 1 && isdirectory(argv()[0]) && !exists("s:std_in") | exe 'NERDTree' argv()[0] | wincmd p | ene | exe 'cd '.argv()[0] | endif

  " map
  nmap <silent> <leader>t :NERDTreeToggle<CR>
  " è¿›å…¥æ–‡ä»¶è‡ªåŠ¨å…³é—­ Tree
  let NERDTreeQuitOnOpen = 0
  " show hidden files, add in 20160125
  let g:NERDTreeShowHidden = 1
  let NERDTreeIgnore = [ '\.obj$', '\.o$', '\.so$', '\.egg$' ]
  "close vim if the only window left open is a NERDTree
  autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTreeType") && b:NERDTreeType == "primary") | q | end
" }}

" https://github.com/tiagofumo/vim-nerdtree-syntax-highlight {{
  " NOTE: https://github.com/tiagofumo/vim-nerdtree-syntax-highlight/issues/30
  let s:git_orange = 'F54D27'
  let g:NERDTreeExtensionHighlightColor = {}
  let g:NERDTreeExtensionHighlightColor['vue'] = '42b883'
  let g:NERDTreeExactMatchHighlightColor = {} " this line is needed to avoid error
  let g:NERDTreeExactMatchHighlightColor['.gitignore'] = s:git_orange " sets the color for .gitignore files
  let g:NERDTreeHighlightFolders = 1 " enables folder icon highlighting using exact match
  let g:NERDTreeHighlightFoldersFullName = 1 " highlights the folder name
" }}


" https://github.com/skywind3000/quickmenu.vim {{
  let g:quickmenu_max_width = bar_width
  let g:quickmenu_ft_blacklist = ['netrw', 'nerdtree']
  let g:quickmenu_disable_nofile = 0
  " choose a favorite key to show / hide quickmenu
  noremap <silent><leader>b :call OpenMenu()<cr>
  " enable cursorline (L) and cmdline help (H)
  let g:quickmenu_options = "L"
  function! OpenMenu()
    call plug#load('quickmenu.vim')
    " Reset Old
    call quickmenu#reset()
    " Build Menu
    call quickmenu#header('Menu Bar')
    call quickmenu#append("# Git", '')
    " use fugitive to show diff
    call quickmenu#append("git diff", 'Gvdiff', "use fugitive's Gvdiff on current document")
    call quickmenu#append("git status", 'Gstatus', "use fugitive's Gstatus on current document")
    " new section
    call quickmenu#append("# Misc", '')
    call quickmenu#append("Light Color Schema", ":colorscheme solarized8_light", "turn on light colorscheme")
    call quickmenu#append("Dark Color Schema", ":colorscheme molokai", "turn on dark colorscheme")
    call quickmenu#append("Wakatime Dashboard", "OpenBrowser https://wakatime.com/dashboard", "go to wakatime")
    " Open it
    call quickmenu#toggle(0)
  endfunction
" }}


" https://github.com/scrooloose/nerdcommenter {{
  " Add spaces after comment delimiters by default
  let g:NERDSpaceDelims = 1
  " Use compact syntax for prettified multi-line comments
  let g:NERDCompactSexyComs = 1
  " Align line-wise comment delimiters flush left instead of following code indentation
  let g:NERDDefaultAlign = 'left'
  " Allow commenting and inverting empty lines (useful when commenting a region)
  let g:NERDCommentEmptyLines = 1
  " Enable trimming of trailing whitespace when uncommenting
  let g:NERDTrimTrailingWhitespace = 1
  " Enable NERDCommenterToggle to check all selected lines is commented or not
  let g:NERDToggleCheckAllLines = 1

  let g:ft = ''
  function! NERDCommenter_before()
    if &ft == 'vue'
      let g:ft = 'vue'
      let stack = synstack(line('.'), col('.'))
      if len(stack) > 0
        let syn = synIDattr((stack)[0], 'name')
        if len(syn) > 0
          exe 'setf ' . substitute(tolower(syn), '^vue_', '', '')
        endif
      endif
    endif
  endfunction

  function! NERDCommenter_after()
    if g:ft == 'vue'
      setf vue
      let g:ft = ''
    endif
  endfunction
" }}


" https://github.com/ctrlpvim/ctrlp.vim {{
  nnoremap <silent> <C-p> :CtrlP<CR>
  " mixed mode is more useful and smart
  let g:ctrlp_cmd = 'CtrlPMixed'
  let g:ctrlp_working_path_mode = 'ra'

  " MacOSX / Linux
  set wildignore+=*/tmp/*,*.so,*.swp,*.zip
  let g:ctrlp_custom_ignore = '\v[\/]\.(git|hg|svn)$'
  let g:ctrlp_custom_ignore = {
        \ 'dir':  '\v[\/]\.(git|hg|svn)$',
        \ 'file': '\v\.(exe|so|dll)$',
        \ 'link': 'some_bad_symbolic_links',
        \ }
  " Ignore files in .gitignore
  let g:ctrlp_user_command = ['.git', 'cd %s && git ls-files -co --exclude-standard']
  let g:ctrlp_follow_symlinks=1
" }}


" https://github.com/dyng/ctrlsf.vim {{
  nnoremap <leader>s :CtrlSF<Space><Right>''<Right><Left>
  let g:ctrlsf_confirm_save = 1
  let g:ctrlsf_auto_close = {
        \ "normal" : 0,
        \ "compact": 0
        \}
  " cwd | project
  let g:ctrlsf_default_root = 'cwd'
  " normal | compact
  let g:ctrlsf_default_view_mode = 'normal'
  " no need
  " let g:ctrlsf_extra_backend_args = {
  "   \ 'ag': '--nogroup --nocolor --column'
  "   \ }
  let g:ctrlsf_search_mode = 'async'
  let g:ctrlsf_position = 'left'
  let g:ctrlsf_winsize = '40%'
  let g:ctrlsf_mapping = {
        \ "next": "n",
        \ "prev": "N",
        \ }
" }}


" https://github.com/airblade/vim-gitgutter {{
  " Required after having changed the colorscheme
  hi clear SignColumn
  let g:gitgutter_map_keys = 0
  nnoremap <leader>] <Plug>GitGutterNextHunk
  nnoremap <leader>[ <Plug>GitGutterPrevHunk
  nnoremap <leader>gh :GitGutterLineHighlightsEnable<CR>
" }}


" https://github.com/leafgarland/typescript-vim {{
  " fix filetype
  autocmd BufNewFile,BufRead *.jsx set filetype=javascript.jsx
  autocmd BufNewFile,BufRead *.tsx set filetype=typescript.tsx
" }}
"

" https://github.com/posva/vim-vue {{
  let g:vue_disable_pre_processors = 1
" }}

" https://github.com/iamcco/markdown-preview.nvim {{
  nnoremap <leader>m :MarkdownPreview<CR>
  nnoremap <leader>M :MarkdownPreviewStop<CR>
" }}


" https://github.com/Valloric/MatchTagAlways {{
  let g:mta_filetypes = {
        \ 'html' : 1,
        \ 'xhtml' : 1,
        \ 'xml' : 1,
        \ 'javascript' : 1,
        \ 'jsx' : 1,
        \ 'typescript' : 1,
        \ 'tsx' : 1,
        \ 'vue' : 1,
        \}
" }}


" https://github.com/heavenshell/vim-jsdoc {{
  nmap <silent> <leader>dd <Plug>(jsdoc)
  let g:jsdoc_allow_input_prompt = 0
  let g:jsdoc_enable_es6 = 1
  let g:jsdoc_access_descriptions = 2
  let g:jsdoc_underscore_private = 1
  let g:jsdoc_custom_args_regex_only = 1
  let g:jsdoc_custom_args_hook = {
        \ '^\(callback\|cb\)$': {
        \   'type': ' {Function} ',
        \   'description': 'Callback function'
        \ },
        \ '\(err\|error\)$': {
        \   'type': '{Error}'
        \ },
        \ '^\(opt\|options\)$': {
        \   'type': '{Object}'
        \ },
        \ 'handler$': {
        \   'type': '{Function}'
        \ },
        \ '^\(n\|i\)$': {
        \   'type': ' {Number} '
        \ },
        \ '^i$': {
        \   'type': ' {Number} '
        \ },
        \ '^num': {
        \   'type': ' {Number} '
        \ },
        \ '^_\?\(is\|has\)': {
        \   'type': ' {Boolean} '
        \ },
        \ '^arr$': {
        \   'type': ' {Array} '
        \ },
        \ '^str$': {
        \   'type': ' {String} '
        \ },
        \ '^e$': {
        \   'type': ' {Event} '
        \ },
        \ 'el$': {
        \   'type': ' {Element} '
        \ },
        \ '^node$': {
        \   'type': ' {Element} '
        \ },
        \ '^o$': {
        \   'type': ' {Object} '
        \ },
        \ '^obj$': {
        \   'type': ' {Object} '
        \ },
        \ '^fn$': {
        \   'type': ' {Function} '
        \ },
        \}
" }}
